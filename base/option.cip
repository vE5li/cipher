export (Option::Some, Option::None)

public enumeration Option<T> {
    Some(T)
    None
}

implement<T> Option<T> {

    public label is_some: Function($self) -> Boolean := {
        match $self {
            case Some -> return implied true
            case None -> return implied false
        }
    }

    public label is_none: Function($self) -> Boolean := {
        match $self {
            case Some -> return implied false
            case None -> return implied true
        }
    }

    public label some: Function($self, function: Function(item: $T)) := {
        if case Some: item == $self {
            function($item)
        }
    }

    public label mutable_some: Function(&self, function: Function(item: &T)) := {
        if case Some: item == &self {
            function(&item)
        }
    }

    public label owned_some: Function(self, function: Function(item: T)) := {
        if case Some: item == self {
            function(item)
        }
    }

    public label none: Function($self, function: Function()) := {
        if case None == $self {
            function()
        }
    }

    public label or: Function(self, other: T) -> Self := {
        match self {
            case Some: item -> return implied Some(item)
            case None -> return implied Some(other)
        }
    }
}

implement<T: Debug> Debug for Option<T> {

    public label debug: Function($self) -> String := {
        match $self {
            case Some: item -> return implied format?("Some({debug})", $item)
            case None -> return implied "None"
        }
    }
}
